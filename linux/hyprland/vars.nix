{ config, lib, pkgs, ... }:

let
  # Machine-specific settings (riva is the Asahi machine)
  isAsahi = config.networking.hostName == "riva";

  # Bar height (waybar) - in pixels, fixed per machine
  barHeight = if isAsahi then 40 else 36;

  # Monitor config
  monitorConfig = ''
    monitor=eDP-1,preferred,auto,1.6
  '' + (if !isAsahi then ''
    monitor=,preferred,auto,1.875
  '' else "");

  # P3 display saturation shader (Asahi only - compensates for wide gamut)
  shaderConfig = if isAsahi then ''
    decoration {
        screen_shader = ~/dev/nixos-config/linux/hyprland/shaders/vibrance-p3.glsl
    }
  '' else "";
in {
  # Generate machine-specific Hyprland variables
  home-manager.users.andrei = { pkgs, config, ... }: {
    xdg.configFile."hypr/vars.conf".text = ''
      # Machine-specific Hyprland variables
      # Generated by NixOS for ${config.networking.hostName or "unknown"}
      $mainMod = SUPER
      $barHeight = ${toString barHeight}

      # Monitor configuration
      ${monitorConfig}
      ${shaderConfig}
    '';

    # Dropdown settings - mutable symlink to repo file
    # Edit the file directly and run `hyprctl reload` to test changes
    xdg.configFile."hypr/dropdown.conf".source =
      config.lib.file.mkOutOfStoreSymlink "/home/andrei/dev/nixos-config/linux/hyprland/dropdown-settings.conf";
  };
}
