{ config, lib, pkgs, ... }:

let
  # Machine-specific settings
  isAsahi = config.networking.hostName == "asahi";

  # Bar height (eww bar)
  barHeight = if isAsahi then 40 else 36;

  # Screen dimensions (effective, after scaling)
  # Asahi: 2560x1664 @ 1.6 = 1600x1040
  # ThinkPad: 2560x1440 @ 1.6 = 1600x900
  screenWidth = 1600;
  screenHeight = if isAsahi then 1040 else 900;

  # Golden ratio (φ ≈ 0.618) dimensions in pixels
  dropdownWidth = builtins.floor (screenWidth * 0.618);   # 988
  dropdownHeight = builtins.floor (screenHeight * 0.618); # 643 or 556
  dropdownX = builtins.floor ((screenWidth - dropdownWidth) / 2); # 306
in {
  # Generate machine-specific Hyprland variables
  home-manager.users.andrei = { pkgs, ... }: {
    xdg.configFile."hypr/vars.conf".text = ''
      # Machine-specific Hyprland variables
      # Generated by NixOS for ${config.networking.hostName}
      $mainMod = SUPER
      $barHeight = ${toString barHeight}
    '';

    # Dropdown window rules (machine-specific due to bar height)
    # Using Hyprland 0.52+ block syntax with pixel values
    xdg.configFile."hypr/dropdown.conf".text = ''
      windowrule {
        name = dropdown-rules
        match:class = dropdown
        workspace = special:dropdown silent
        float = 1
        size = ${toString dropdownWidth} ${toString dropdownHeight}
        move = ${toString dropdownX} ${toString (barHeight * 2)}
      }
    '';
  };
}
